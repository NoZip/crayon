%{
#include <iostream>

#include "error.hpp"

#include "point.hpp"
#include "path.hpp"

#include "environment.hpp"
#include "expression.hpp"
#include "point_expression.hpp"
#include "inequation.hpp"

#include "instruction.hpp"
#include "command.hpp"
#include "variable_affectation.hpp"
#include "flow.hpp"

#include "for.hpp"
#include "while.hpp"
#include "conditional.hpp"

#define YYLEX_PARAM &yylval, &yylloc

using std::string;
%}

%locations

// Types definitions

%union {
    float real;
    bool boolean;
    char* name;
    PointExpression* point;
    Path* path;
    Expression* expression;
    Inequation* inequation;
    Instruction* instruction;
    Command *command;
    Flow* flow;
    // Conditional* conditional;
    // While* while;
}

%token <real> REAL
%token <name> NAME

%type <expression> sexpression
%type <expression> expression
%type <inequation> inequation

%type <point> spoint
%type <point> point
%type <path> path

%type <instruction> instruction
%type <instruction> command
%type <instruction> variable
%type <flow> flow
%type <flow> block
%type <flow> image
%type <instruction> for
%type <instruction> while
%type <instruction> conditional


// Operators and separators
%token SEMICOLON
%token EQ
%token RPAR LPAR
%token PLUS MINUS MUL DIV
%token EQUAL NEQUAL GT GTE LT LTE
%token NOT OR AND
%token COMMA DOUBLE_DOT
%token PATH_SEPARATOR CYCLE
%token DRAW_COMMAND FILL_COMMAND
%token TRANSLATE_BUILDIN
%token ROTATE_BUILDIN
%token LBRACE
%token RBRACE
%token VAR
%token IMAGE
%token FOR
%token IF
%token ELSE
%token WHILE

%left PLUS MINUS
%left MUL DIV
%left OR
%left AND
%left UNARY

%{
#define yyerror(Msg) _yyerror(&yylloc, Msg)

void _yyerror(YYLTYPE *yylloc, const char *s) {
    fprintf(stderr, "ERROR %d:%d %s\n", yylloc->first_line, yylloc->first_column, s);
}

extern "C" {
    int yylex(YYSTYPE * yylval_param,YYLTYPE * yylloc_param);
}

Environment current_env;
%}

%%

root : flow {$1->execute(current_env); delete $1;}
;

instruction : command SEMICOLON {$$ = $1;}
| variable SEMICOLON {$$ = $1;}
| block {$$ = $1;}
| image {$$ = $1;}
| for {$$ = $1;}
// | while {$$ = $1;}
| conditional {$$ = $1;}
;

flow : flow instruction {$$ = $1; $$->append($2);}
| instruction {Flow *f = new Flow(); f->append($1); $$ = f;}
;

expression : NAME {$$ = new VariableExpression(string($1));}
| sexpression

// Computes mathematical expressions
sexpression : expression PLUS expression {$$ = new BinaryExpression(EXPR_PLUS, $1, $3);}
| expression MINUS expression {$$ = new BinaryExpression(EXPR_MINUS, $1, $3);}
| MINUS expression %prec UNARY {$$ = new UnaryExpression(EXPR_NEGATE, $2);}
| expression MUL expression {$$ = new BinaryExpression(EXPR_MUL, $1, $3);}
| expression DIV expression {$$ = new BinaryExpression(EXPR_DIV, $1, $3);}
| REAL {$$ = new ConstantExpression($1);}
| RPAR expression LPAR {$$ = $2;}
;

inequation : expression EQUAL expression {$$ = new BinaryInequation(INEQ_EQUAL, $1, $3);}
| expression NEQUAL expression {$$ = new BinaryInequation(INEQ_NEQUAL, $1, $3);}
| expression GT expression {$$ = new BinaryInequation(INEQ_GT, $1, $3);}
| expression GTE expression {$$ = new BinaryInequation(INEQ_GTE, $1, $3);}
| expression LT expression {$$ = new BinaryInequation(INEQ_LT, $1, $3);}
| expression LTE expression {$$ = new BinaryInequation(INEQ_LTE, $1, $3);}
| NOT inequation %prec UNARY {$$ = new UnaryInequation(INEQ_NOT, $2);}
| inequation AND inequation {$$ = new CompositeInequation(INEQ_AND, $1, $3);}
| inequation OR inequation {$$ = new CompositeInequation(INEQ_OR, $1, $3);}
| RPAR inequation LPAR {$$ = $2;} 
;

point : NAME {$$ = new VarPointExpression(string($1));}
| spoint

spoint : RPAR expression COMMA expression LPAR {
    $$ = new ConstantPointExpression($2, $4);
}
/*
| RPAR expression DOUBLE_DOT expression LPAR {
    $$ = new ConstantPointExpression(Point::from_polar($2, $4));
}
*/
/*
| TRANSLATE_BUILDIN RPAR point COMMA point LPAR {
  $$ = new Point($3->translate(*$5));
}
| ROTATE_BUILDIN RPAR point COMMA point COMMA REAL LPAR {
  $$ = new Point($3->rotate(*$5,$7));
}
*/
;

path : path PATH_SEPARATOR point {
    $1->append($3);
    $$ = $1;
}
| path PATH_SEPARATOR PLUS point {
    $1->append_relative($4);
    $$ = $1;
}
| path PATH_SEPARATOR CYCLE {
    $1->append_cycle();
    $$ = $1;
}
| point PATH_SEPARATOR point {
    Path *c = new Path();
    c->append($1);
    c->append($3);
    $$ = c;
}
| point PATH_SEPARATOR PLUS point {
    Path *c = new Path();
    c->append($1);
    c->append_relative($4);
    $$ = c;
}
| point PATH_SEPARATOR CYCLE {
    Path *c = new Path();
    c->append($1);
    c->append_cycle();
    $$ = c;
}
;

command : DRAW_COMMAND path {
    Command *c = new Command(DRAW, $2);
    $$ = c;
    // c->execute(*current_env);
}
| DRAW_COMMAND NAME {
    $$ = new VariableCommand(DRAW, string($2));
}
| FILL_COMMAND path {
    Command *c = new Command(FILL, $2);
    $$ = c;
}
/*
| FILL_COMMAND NAME {
    Variable v = current_env->get_variable(string($2));

    if (v.type != PATH)
        throw "MOTHERFUCKING ERROR!";
    
    Path p = *((Path*) v.value);

    Command *c = new Command(FILL, p);
    $$ = c;
    // c->execute(*current_env);
}
*/
;

variable: NAME EQ NAME {$$ = new VariableAffectation(string($1), SCALAR, new ConstantExpression(0));}
| VAR NAME EQ sexpression {$$ = new VariableInitialization(string($2), SCALAR, $4);}
| NAME EQ sexpression {$$ = new VariableAffectation(string($1), SCALAR, $3);}
| NAME PLUS EQ sexpression {$$ = new VariableIncrement(string($1), EXPR_PLUS, $4);}
| NAME EQ spoint {$$ = new VariableAffectation(string($1), VECTOR2D, $3);}
| VAR NAME EQ spoint {$$ = new VariableInitialization(string($2), VECTOR2D, $4);}
| NAME EQ path {$$ = new VariableAffectation(string($1), PATH_TYPE, $3);}
| VAR NAME EQ path {$$ = new VariableInitialization(string($2), PATH_TYPE, $4);}
| NAME EQ image {$$ = new VariableAffectation(string($1), IMAGE_TYPE, $3);}
| VAR NAME EQ image {$$ = new VariableInitialization(string($2), IMAGE_TYPE, $4);}
;

block : LBRACE flow RBRACE {$$ = $2;}

for : FOR RPAR variable SEMICOLON inequation SEMICOLON variable LPAR block {$$ = new For($3, $5, $7, $9);}

while : WHILE RPAR inequation LPAR block {$$ = new While($3, $5);}

conditional : IF RPAR inequation LPAR block ELSE block {$$ = new Conditional($3, $5, $7);}
| IF RPAR inequation LPAR block {$$ = new Conditional($3, $5);}
;

image: IMAGE block {$$ = $2;}
;

/*
rotation : ROTATION RPAR REAL COMMA REAL COMMA REAL LPAR

*/

%%

int main(int argc, char ** argv) {
    Command::init_cairo(CAIRO_FORMAT_ARGB32, 200, 200);
    yyparse();
    Command::write_png_file(argv[1]);
    Command::close_cairo();

    return EXIT_SUCCESS;
}
